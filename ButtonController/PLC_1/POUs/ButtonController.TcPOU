<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="ButtonController" Id="{b96121b6-e5f7-4f75-84d9-29f0bf50c622}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK ButtonController
VAR_INPUT
	Start : BOOL;
	Reset : BOOL;
	Stop : BOOL;
	EmergencyStop : BOOL;
END_VAR
VAR_OUTPUT
	StartLight : BOOL;
	ResetLight : BOOL;
	StopLight : BOOL;
END_VAR
VAR
	tBlink : TON;
	BlinkBit : BOOL;
	state : INT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// I/O assignment
GVL.mb_Input_Coils[1] := StartLight;
GVL.mb_Input_Coils[2] := ResetLight;
GVL.mb_Input_Coils[3] := StopLight;
Start := gvl.mb_Output_Coils[3];
Reset := gvl.mb_Output_Coils[4];
Stop := gvl.mb_Output_Coils[5];
EmergencyStop := gvl.mb_Output_Coils[7];
//
// Blinking logic
tBlink(IN := NOT tBlink.Q, PT := T#1000MS);
BlinkBit := tBlink.ET > T#500MS;
//
CASE state OF
	0: // Idle (stop)
		StartLight := BlinkBit;
		StopLight := TRUE;
		ResetLight := FALSE;
		IF Start THEN
			state := 1;
		END_IF;
		IF NOT EmergencyStop THEN
			state := 2;
		END_IF;
	1: // Running state
		StartLight := TRUE;
		StopLight := FALSE;
		ResetLight := FALSE;
		IF NOT EmergencyStop THEN
			state := 2;
		END_IF;
		IF NOT Stop THEN
			state := 0;
		END_IF;
	2: // Emergency Stop
		StopLight := TRUE;
		StartLight := FALSE;
		IF EmergencyStop THEN
			ResetLight := BlinkBit;
			IF Reset THEN
				state := 3;
			END_IF;
		ELSE
			ResetLight := FALSE;
		END_IF;
	3: // After emergency stop
		StopLight := FALSE;
		ResetLight := TRUE;
		StartLight := BlinkBit;
		IF Start THEN
			state := 1;
		END_IF;
		IF NOT EmergencyStop THEN
			state := 2;
		END_IF;
ELSE
	state := 0;
END_CASE;]]></ST>
    </Implementation>
    <LineIds Name="ButtonController">
      <LineId Id="39" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="31" Count="4" />
      <LineId Id="37" Count="0" />
      <LineId Id="44" Count="1" />
      <LineId Id="47" Count="1" />
      <LineId Id="141" Count="1" />
      <LineId Id="187" Count="7" />
      <LineId Id="186" Count="0" />
      <LineId Id="152" Count="32" />
      <LineId Id="51" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>